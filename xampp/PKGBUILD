# Maintainer: Jose Valecillos <valecillosjg (at) gmail (dot) com>
# Contributor: Thiago Perrotta <echo dGhpYWdvcGVycm90dGE5NUBnbWFpbC5jb20K | base64 -d >
pkgname=xampp
pkgver=1.8.3
pkgrel=2
pkgdesc="A free and open source cross-platform web server package (LAMP Stack), consisting mainly of the Apache HTTP Server, MySQL database, and interpreters for scripts written in the PHP and Perl programming languages"
url="http://www.apachefriends.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('net-tools')
makedepends=('fakeuser' 'proot')
[ "$CARCH" = "i686" ]   && source=("http://sourceforge.net/projects/${pkgname}/files/XAMPP%20Linux/${pkgver}/${pkgname}-linux-${pkgver}-4-installer.run/download")
[ "$CARCH" = "x86_64" ] && source=("http://sourceforge.net/projects/${pkgname}/files/XAMPP%20Linux/${pkgver}/${pkgname}-linux-x64-${pkgver}-4-installer.run/download")
options=(!strip)
install=xampp.install
md5sums=('SKIP')

_warning() {
  echo "Due to limitations of the .run file of this package, you now must"
  echo "have a mysql user in your system prior to its build."

  echo "You can either create a mysql user manually or just run fakepkg"
  echo "instead of makepkg. You can pass normal makepkg parameters to it,"
  echo "such as fakepkg -si".
}

package() {
  _warning

  install -dm755 "${pkgdir}/opt/lampp"

  msg "Creating a temporary mysql user/group with fakeadd(fakeuser)..."
  getent group  mysql >/dev/null || fakeadd -G -n mysql -g 713        || _warning
  getent passwd mysql >/dev/null || fakeadd -U -n mysql -g 713 -u 713 || _warning

  msg "Extracting package to a chroot..."
  chmod +x "${srcdir}"/download
  echo "Y" | proot -b "${pkgdir}"/opt/lampp:/opt/lampp "${srcdir}"/download --mode text
  chmod g-s -R "${pkgdir}"/opt/lampp

  # licenses
  install -dm755 "${pkgdir}"/usr/share/licenses/xampp
  cp "${pkgdir}"/opt/lampp/licenses/* "${pkgdir}"/usr/share/licenses/xampp

  # /usr/bin executables
  install -dm755 "${pkgdir}"/usr/bin
  ln -sf /opt/lampp/lampp "${pkgdir}"/usr/bin/xampp
  ln -sf /opt/lampp/lampp "${pkgdir}"/usr/bin/lampp
}
