# Maintainer: Thiago Perrotta <echo dGhpYWdvcGVycm90dGE5NUBnbWFpbC5jb20K | base64 -d >
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=translate-shell
pkgver=0.8.22.5
pkgrel=3
pkgdesc="Google Translate to serve as a command-line tool."
arch=('any')
url="http://www.soimort.org/$pkgname"
license=('Public Domain')
depends=('gawk>=4.0')
makedepends=('git')
optdepends=(
#   'zsh: for interpreting the wrapper script'
    'fribidi: needed for displaying right-to-left (RTL) languages'
    'mplayer: needed for the Text-to-Speech functionality'
    'espeak: needed for the Text-to-Speech functionality'
    'rlwrap: needed for readline-style editing and history in the interactive mode'
    'emacs: for using the Emacs interface (with the -E flag)'
)
provides=("$pkgname")
conflicts=("$pkgname-git")
install=$pkgname.install
source=("https://github.com/soimort/$pkgname/archive/v$pkgver.tar.gz")
md5sums=('0e73b25c0b2ead022268555943a77460')
sha256sums=('a0e3d5ac173f8964d05e698cb27628852a98e77cbb31006bbee54f8ff9e2d7d8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p "$pkgdir/usr/bin/"

  # this version works with bash only by default
  # change it to TARGET=zsh if you want to use it with zsh only
  # or, alternatively, execute '/usr/bin/trans' instead of 'trans'
  # for more information, please read the .install file
  make TARGET=bash "INSTDIR=$pkgdir/usr/bin" install

  install -Dm644 man/trans.1 "$pkgdir/usr/share/man/man1/trans.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
